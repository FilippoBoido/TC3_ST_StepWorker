<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.2">
  <POU Name="FB_StepWorker" Id="{44f74475-c942-48f3-81b1-525d23658d36}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK PUBLIC FB_StepWorker IMPLEMENTS I_StepWorker
VAR_INPUT
	bResetTimeout : BOOL;
END_VAR
VAR_OUTPUT
	uiStep : UINT;
	bTimeout : BOOL;
END_VAR
VAR
	uiConditionCounter : UINT := 1;
	aConditions	: ARRAY[1..cMaxConditions] OF I_StepCondition;

	bTimeoutFlag : BOOL;	
	fbTimeout : TON;
	fbTimer : TON;
	tTimeIntern : TIME;
END_VAR
VAR CONSTANT
	cMaxConditions : UINT := 25;
	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF bResetTimeout
THEN
	bResetTimeout := FALSE;
	bTimeout := FALSE;
	bTimeoutFlag := FALSE;
END_IF

fbTimeout(IN:=bTimeoutFlag, PT:=tTimeIntern , Q=>bTimeout);
fbTimer();]]></ST>
    </Implementation>
    <Method Name="AndNegCondition" Id="{6f316bf1-42e4-4e90-a61a-7baff43334c0}">
      <Declaration><![CDATA[METHOD AndNegCondition : I_StepWorker
VAR_INPUT
	itfStepCondition : I_StepCondition;
END_VAR
VAR
	i : UINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[andNegCondition := THIS^;
IF itfStepCondition = 0 //Avoid null pointer exception.
	OR uiConditionCounter = cMaxConditions + 1
THEN
	
	RETURN;
END_IF

FOR i := 1 TO uiConditionCounter DO
	IF aConditions[i] = itfStepCondition 
	THEN
		//Condition already added.
		RETURN;
	END_IF
END_FOR
 
//Add new condition.
itfStepCondition.NegCondition();
aConditions[uiConditionCounter] := itfStepCondition;
uiConditionCounter := uiConditionCounter + 1;

]]></ST>
      </Implementation>
    </Method>
    <Method Name="AndPosCondition" Id="{8ec1ae46-390d-491a-806a-a2abcd8e50a5}">
      <Declaration><![CDATA[METHOD AndPosCondition : I_StepWorker
VAR_INPUT
	itfStepCondition : I_StepCondition;
END_VAR
VAR
	i : UINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[andPosCondition := THIS^;
IF itfStepCondition = 0 //Avoid null pointer exception.
	OR uiConditionCounter = cMaxConditions + 1
THEN
	
	RETURN;
END_IF

FOR i := 1 TO uiConditionCounter DO
	IF aConditions[i] = itfStepCondition 
	THEN
		//Condition already added.
		RETURN;
	END_IF
END_FOR
 
//Add new condition.
itfStepCondition.PosCondition();
aConditions[uiConditionCounter] := itfStepCondition;
uiConditionCounter := uiConditionCounter + 1;

]]></ST>
      </Implementation>
    </Method>
    <Method Name="NextStep" Id="{fbdecb39-2f5a-4df6-a2c8-cb28af62eebe}">
      <Declaration><![CDATA[METHOD NextStep : UINT
VAR_INPUT
	uiNextStep : UINT;
END_VAR
VAR
	i : UINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF bTimeout
THEN
	NextStep := uiStep;
	RETURN;
END_IF
	
FOR i := 1 TO (uiConditionCounter-1) DO
	IF aConditions[i].isNegative
	THEN
		IF aConditions[i].getValue = TRUE 
		THEN
			//one Condition is True therefor exit.
			NextStep := uiStep;
			RETURN;
		END_IF
	ELSIF aConditions[i].isPositive
	THEN
		IF aConditions[i].getValue = FALSE 
		THEN
			//one Condition is false therefor exit.
			NextStep := uiStep;
			RETURN;
		END_IF
	END_IF
	
	
END_FOR

//All conditions met
bTimeoutFlag := FALSE;

//Reset the condition counter and conditions
FOR i := 1 TO (uiConditionCounter-1) DO
	aConditions[i] := 0;
END_FOR

uiConditionCounter := 1;

IF fbTimer.IN 
	AND fbTimer.Q
THEN
	fbTimer.IN := FALSE;
	uiStep := uiNextStep;
	NextStep := uiNextStep;
ELSIF fbTimer.IN
	AND NOT fbTImer.Q
THEN
	NextStep := uiStep;
ELSE
	uiStep := uiNextStep;
	NextStep := uiNextStep;	
END_IF



]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetTimeout" Id="{fe6f174f-06ef-4ced-b025-bdae118f56fe}">
      <Declaration><![CDATA[METHOD SetTimeout : I_StepWorker
VAR_INPUT
	tTime : TIME;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SetTimeout := THIS^;
tTimeIntern := tTime;
bTimeoutFlag := TRUE;


]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetTimer" Id="{feb9bbef-be60-4033-bb9b-8d4f4516e849}">
      <Declaration><![CDATA[METHOD SetTimer : I_StepWorker
VAR_INPUT
	tTime : TIME;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[SetTimer := THIS^;
fbTimer.PT := tTime;
fbTimer.IN := TRUE;]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_StepWorker">
      <LineId Id="30" Count="6" />
      <LineId Id="9" Count="0" />
      <LineId Id="58" Count="0" />
    </LineIds>
    <LineIds Name="FB_StepWorker.AndNegCondition">
      <LineId Id="6" Count="20" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_StepWorker.AndPosCondition">
      <LineId Id="6" Count="16" />
      <LineId Id="33" Count="0" />
      <LineId Id="23" Count="2" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_StepWorker.NextStep">
      <LineId Id="48" Count="0" />
      <LineId Id="50" Count="0" />
      <LineId Id="60" Count="0" />
      <LineId Id="53" Count="0" />
      <LineId Id="52" Count="0" />
      <LineId Id="51" Count="0" />
      <LineId Id="6" Count="0" />
      <LineId Id="92" Count="1" />
      <LineId Id="96" Count="4" />
      <LineId Id="95" Count="0" />
      <LineId Id="101" Count="1" />
      <LineId Id="104" Count="4" />
      <LineId Id="103" Count="0" />
      <LineId Id="94" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="32" Count="0" />
      <LineId Id="36" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="70" Count="0" />
      <LineId Id="69" Count="0" />
      <LineId Id="73" Count="4" />
      <LineId Id="79" Count="6" />
      <LineId Id="72" Count="0" />
      <LineId Id="78" Count="0" />
      <LineId Id="62" Count="0" />
      <LineId Id="14" Count="0" />
      <LineId Id="17" Count="0" />
    </LineIds>
    <LineIds Name="FB_StepWorker.SetTimeout">
      <LineId Id="36" Count="4" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_StepWorker.SetTimer">
      <LineId Id="16" Count="1" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>